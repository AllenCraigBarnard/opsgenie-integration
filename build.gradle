buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:2.0.3'
    }
}

plugins {
    id "nebula.os-package-base" version "2.0.3"
}
apply plugin: 'java'
apply plugin: 'groovy'

sourceCompatibility = 1.6
targetCompatibility = 1.6

group = "com.ifountain.opsgenie-integration"

project.ext.versions = new Properties();
project.ext.versions.load(new FileInputStream("${projectDir}/version.properties"))
if(gradle.startParameter.taskNames.contains('removeSnapshot')) {
    project.ext.versionSuffix = ''
} else {
    project.ext.versionSuffix = '-SNAPSHOT'
}

version = project.ext.versions.getProperty('client') + project.ext.versionSuffix

sourceSets {
    main {
        groovy {
            include "*/marid/scripts"
            include "*/lamp/scripts"
        }
    }
}


dependencies {
    compile project(":lamp")
    compile project(":marid")
}

subprojects  {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    group = "com.opsgenie.integration"
    version = rootProject.version

    repositories {
        mavenCentral()
        maven {
            url "http://repo.opsgenie.com:9393/content/groups/public/"
        }
    }

    test.onlyIf {
        false
    }

    jar {
        manifest {
            attributes("Built-By": "http://www.opsgenie.com",
                    "Specification-Title": project.name[0].toUpperCase() + project.name[1..-1],
                    "Specification-Product": project.name,
                    "Specification-Version": project.version,
                    "Specification-Vendor": "http://www.opsgenie.com",
                    "Implementation-Title" : project.name[0].toUpperCase() + project.name[1..-1],
                    "Implementation-Product": project.name,
                    "Implementation-Vendor":"http://www.opsgenie.com",
                    "Implementation-Version":project.version,
            )
        }
    }

    dependencies {
        compile 'log4j:log4j:1.2.16'
    }

}

repositories {
    mavenCentral()
    maven {
        url "http://repo.opsgenie.com:9393/content/groups/public/"
    }
}

configurations {
    netcool
    smarts
    splunk
    debian
}

dependencies {
    netcool 'com.sybase.jdbc:sybase:5.5.OG'
    smarts 'com.smarts:net:1.0.OG'
    smarts 'com.smarts:platform:1.0.OG'
    smarts 'com.smarts:skclient:1.0.OG'
    splunk 'org.jfree:jcommon:1.0.17'
    splunk 'org.jfree:jfreechart:1.0.14'
    splunk 'org.apache.commons:commons-csv:1.0'
    debian 'org.vafer:jdeb:1.3'
}

task generateIntegration(dependsOn: 'package:generateBaseForIntegration') {}

task generateIntegrationForNative(dependsOn: 'generateIntegration') << {
    copy {
        from("${project(':common').projectDir}/native")
        into("${project.buildDir}/native")
    }
}

def mergeConfFile(String mainConfFile, String partialConfFilePath, String destinationFilePath){
    def partialConfFile = new File(partialConfFilePath)
    String wholeConfFile = new File(mainConfFile).getText() +"\n"+ partialConfFile.getText();
    def destFile = new File(destinationFilePath)
    destFile.setText(wholeConfFile);
}

def copyNativeFiles(String sourceDir) {
    copy {
        from("${project.buildDir}/native")
        into("${project.buildDir}/os_client/${sourceDir}")
    }
}

def buildRpm(String sourceDir, String name, String descrpt, Map fileMappingInput, Map configFilesInput) {
    def taskName = "generateRpm${sourceDir}"

    def configFiles = [:]
    configFiles.putAll(configFilesInput)
    def fileMapping = [:]
    fileMapping.putAll(fileMappingInput)

    task "${taskName}"(type: Rpm) {

        def nativeSource = "${project.buildDir}/os_client/${sourceDir}";

        def packageVersion = project.ext.versions.getProperty(sourceDir) + project.ext.versionSuffix

        packageName = name
        description = descrpt
        summary = descrpt
        packageDescription = descrpt + ". Please see doc for details: https://www.opsgenie.com/docs/"
        version = packageVersion.replace("-", ".");
        os = LINUX
        user = 'root'

        release = '1.all'
        packager = "ifountain"
        vendor = "opsgenie"
        url = "https://www.opsgenie.com"
        license = "Apache License 2.0"
        buildHost = "repo.opsgenie.com"
        packageGroup = "System Administration Tools"


        configFiles.put("${project.buildDir}/${sourceDir}/opsgenie-integration/conf/opsgenie-integration.conf", 'etc/opsgenie/conf')
        configFiles.put("${project.buildDir}/${sourceDir}/opsgenie-integration/lamp/conf/log.properties", 'etc/opsgenie/lamp')
        configFiles.put("${project.buildDir}/${sourceDir}/opsgenie-integration/marid/conf/log.properties", 'etc/opsgenie/marid')

        fileMapping.put("${project.buildDir}/${sourceDir}/opsgenie-integration/lamp/lib", 'var/lib/opsgenie/lamp')
        fileMapping.put("${project.buildDir}/${sourceDir}/opsgenie-integration/lamp/scripts", 'var/opsgenie/lamp/scripts')
        fileMapping.put("${project.buildDir}/${sourceDir}/opsgenie-integration/marid/scripts", 'var/opsgenie/marid/scripts')
        fileMapping.put("${project.buildDir}/${sourceDir}/opsgenie-integration/marid/conf/.keystore", 'etc/opsgenie/marid')
        fileMapping.put("${project.buildDir}/${sourceDir}/opsgenie-integration/marid/lib", 'var/lib/opsgenie/marid')

        for (def source : configFiles.keySet()) {
            def dest = configFiles.get(source)
            logger.info("config " + source + " - " + dest)
            from(source) {
                fileType CONFIG
                into(dest)
            }
        }

        for(def source : fileMapping.keySet()) {
            def dest = fileMapping.get(source)
            logger.info(source + " - " + dest)
            from(source) {
                into(dest)
            }
        }

        from("${nativeSource}/lamp_rpm"){
            into("usr/bin")
            rename "lamp_rpm", "lamp"
        }

        from("${nativeSource}/profile"){
            into("etc/opsgenie")
        }

        from("${nativeSource}/marid_rpm"){
            into("etc/init.d")
            rename 'marid_rpm', 'marid'
        }

        preInstall file("${nativeSource}/ogBefore.sh")
        postInstall file("${nativeSource}/ogAfter.sh")
    }

    tasks[taskName].execute()
}

def buildDeb(String sourceDir, String name, String descrpt, Map fileMappingInput, Map configFilesInput) {

    def packageVersion = project.ext.versions.getProperty(sourceDir) + project.ext.versionSuffix
    packageVersion = packageVersion.replace("-", ".")

    def taskName = "prepareDeb${sourceDir}"
    def dataDir = "${project.buildDir}/debian_tmp/${sourceDir}/data"
    def controlDir = "${project.buildDir}/debian_tmp/${sourceDir}/control"

    def configFiles = [:]
    configFiles.putAll(configFilesInput)
    def fileMapping = [:]
    fileMapping.putAll(fileMappingInput)

    task "${taskName}" << {

        def nativeSource = "${project.buildDir}/os_client/${sourceDir}";

        fileMapping.put("${project.buildDir}/${sourceDir}/opsgenie-integration/conf/opsgenie-integration.conf", 'etc/opsgenie/conf')
        fileMapping.put("${project.buildDir}/${sourceDir}/opsgenie-integration/lamp/log.properties", 'etc/opsgenie/lamp')
        fileMapping.put("${project.buildDir}/${sourceDir}/opsgenie-integration/marid/log.properties", 'etc/opsgenie/marid')

        fileMapping.put("${project.buildDir}/${sourceDir}/opsgenie-integration/lamp/conf", 'etc/opsgenie/lamp')
        fileMapping.put("${project.buildDir}/${sourceDir}/opsgenie-integration/lamp/lib", 'var/lib/opsgenie/lamp')
        fileMapping.put("${project.buildDir}/${sourceDir}/opsgenie-integration/lamp/scripts", 'var/opsgenie/lamp/scripts')
        fileMapping.put("${project.buildDir}/${sourceDir}/opsgenie-integration/marid/scripts", 'var/opsgenie/marid/scripts')
        fileMapping.put("${project.buildDir}/${sourceDir}/opsgenie-integration/marid/conf", 'etc/opsgenie/marid')
        fileMapping.put("${project.buildDir}/${sourceDir}/opsgenie-integration/marid/lib", 'var/lib/opsgenie/marid')
        fileMapping.put("${project.buildDir}/${sourceDir}/opsgenie-integration/conf", 'etc/opsgenie/conf')

        for(def source : fileMapping.keySet()) {
            def dest = fileMapping.get(source)
            logger.info(source + " - " + dest)
            copy {
                from(source)
                into("${dataDir}/${dest}")
            }
        }

        copy {
            from("${buildDir}/os_client/${sourceDir}/conffiles_deb")
            into("${controlDir}")
            rename "conffiles_deb", 'conffiles'
        }

        //conf files
        StringBuilder appender = new StringBuilder()
        for (def source : configFiles.keySet()) {
            def dest = configFiles.get(source)
            logger.info(source + " - " + dest)
            copy {
                from(source)
                into("${dataDir}/${dest}")
            }

            appender.append("/${dest}/${source.tokenize('/')[-1]}\n")
        }

        def conffiles = new File("${controlDir}/conffiles")
        conffiles.append("\n" + appender.toString())

        copy {
            from("${nativeSource}/lamp_rpm")
            into("${dataDir}/usr/bin")
            rename "lamp_rpm", "lamp"
        }

        copy {
            from("${nativeSource}/profile")
            into("${dataDir}/etc/opsgenie")
        }

        copy {
            from("${nativeSource}/marid_deb")
            into("${dataDir}/etc/init.d")
            rename 'marid_deb', 'marid'
        }

        copy {
            from "${nativeSource}/control_deb"
            filter {
                it.replace('[description]', descrpt)
                        .replace('[version]', packageVersion)
                        .replace('[package-name]', "${name}")
            }
            into controlDir
            rename "control_deb", "control"
        }

        copy {
            from "${nativeSource}/ogBefore.sh"
            into controlDir
            rename "ogBefore.sh", "preinst"
        }

        copy {
            from "${nativeSource}/ogAfter.sh"
            into controlDir
            rename "ogAfter.sh", "postinst"
        }

        ant {
            taskdef(name: 'deb', classname: 'org.vafer.jdeb.ant.DebAntTask', classpath: configurations.debian.asPath)
        }
        ant.deb(destfile: "${project.buildDir}/distributions/${name}_${packageVersion}_all.deb", control: controlDir) {
            ant.data(src: dataDir, type: 'directory', dst: '/')
        }

        delete "${project.buildDir}/distributions/${name}_${packageVersion}_all.changes"
    }

    tasks[taskName].execute()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}

apply from: 'release.gradle'
apply from: 'sdk.gradle'

//apply scripts
apply from: 'smarts/smarts.gradle'
apply from: 'splunk/splunk.gradle'
apply from: 'opennms/opennms.gradle'
apply from: 'nagios/nagios.gradle'
apply from: 'nagiosxi/nagiosxi.gradle'
apply from: 'netcool/netcool.gradle'
apply from: 'zabbix/zabbix.gradle'
apply from: 'redmine/redmine.gradle'
apply from: 'zenoss/zenoss.gradle'
apply from: 'icinga/icinga.gradle'
apply from: 'zendesk/zendesk.gradle'
apply from: 'icinga2/icinga2.gradle'

task packageAll(dependsOn: ['packageSmarts',
                               'packageSplunk',
                               'packageOpennms',
                               'packageNagios',
                               'packageNagiosxi',
                               'packageNetcool',
                               'packageZabbix',
                               'packageRedmine',
                               'packageZenoss',
                               'packageIcinga',
                                'packageZendesk',
                                'packageIcinga2'
])
